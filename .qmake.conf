VERSION = 2.2.0

CONFIG += warning_clean exceptions
android: CONFIG += c++14
DEFINES += QT_DEPRECATED_WARNINGS QT_ASCII_CAST_WARNINGS QT_USE_QSTRINGBUILDER


PROJECT_TARGET = remind-me
QMAKE_TARGET_COMPANY = "Skycoder42"
QMAKE_TARGET_DESCRIPTION = "Simple reminder app"
QMAKE_TARGET_COPYRIGHT = "Felix Barz"
QMAKE_TARGET_BUNDLE_PREFIX = de.skycoder42

DEFINES += "PROJECT_TARGET=\\\"$$PROJECT_TARGET\\\""
DEFINES += "VERSION=\\\"$$VERSION\\\""
DEFINES += "COMPANY=\"\\\"$$QMAKE_TARGET_COMPANY\\\"\""
DEFINES += "BUNDLE=\"\\\"$$QMAKE_TARGET_BUNDLE_PREFIX\\\"\""

# installing
android: INSTALL_TRANSLATIONS = /assets/translations

exists($$PWD/install.pri):include($$PWD/install.pri)
isEmpty(INSTALL_PREFIX) {
	INSTALL_PREFIX = $$[QT_INSTALL_PREFIX]
	isEmpty(INSTALL_BINS): INSTALL_BINS = $$[QT_INSTALL_BINS]
	isEmpty(INSTALL_LIBS): INSTALL_LIBS = $$[QT_INSTALL_LIBS]
	isEmpty(INSTALL_HEADERS): INSTALL_HEADERS = $$[QT_INSTALL_HEADERS]
	isEmpty(INSTALL_PLUGINS): INSTALL_PLUGINS = $$[QT_INSTALL_PLUGINS]
	isEmpty(INSTALL_TRANSLATIONS): INSTALL_TRANSLATIONS = $$[QT_INSTALL_TRANSLATIONS]
} else {
	isEmpty(INSTALL_BINS): INSTALL_BINS = $${INSTALL_PREFIX}/bin
	isEmpty(INSTALL_LIBS): INSTALL_LIBS = $${INSTALL_PREFIX}/lib
	isEmpty(INSTALL_HEADERS): INSTALL_HEADERS = $${INSTALL_PREFIX}/include
	isEmpty(INSTALL_PLUGINS): INSTALL_PLUGINS = $${INSTALL_PREFIX}/plugins
	isEmpty(INSTALL_TRANSLATIONS): INSTALL_TRANSLATIONS = $${INSTALL_PREFIX}/translations
}
isEmpty(INSTALL_SHARE): INSTALL_SHARE = $${INSTALL_PREFIX}/share
